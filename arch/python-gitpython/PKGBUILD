# vim: set ts=4 sw=4 sts=4 et:
# Maintainer: Jeremy Asuncion <jeremyasuncion808@gmail.com>

_name='GitPython'
pkgname=('python-gitpython' 'python2-gitpython')
pkgbase=python-gitpython
pkgver=2.1.0
pkgrel=1
pkgdesc='GitPython is a python library used to interact with Git repositories.'
arch=(any)
url="https://github.com/gitpython-developers/GitPython"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/d7/16/e914d345d7d4e988f9196b9719a99220bad6a5dbd636162f9b5cc35f3bd6/${_name}-${pkgver}.tar.gz")
md5sums=('29b1fcf504d080dc7a5e630957e829d7')

prepare() {
    cp -a "${srcdir}/${_name}-${pkgver}"{,'-python2'}
}

build() {
    cd "${srcdir}/${_name}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_name}-${pkgver}-python2"
    python2 setup.py build
}

package_python-gitpython() {
    depends=('python')
    cd "${srcdir}/${_name}-${pkgver}"
    python setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}" --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-gitpython/LICENSE"
}

package_python2-gitpython() {
    depends=('python2')
    cd "${srcdir}/${_name}-${pkgver}-python2"
    python2 setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}" --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python2-gitpython/LICENSE"
}

