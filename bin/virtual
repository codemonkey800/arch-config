#!/usr/bin/env fish

function help
    echo 'Boostrap development environment using latest dev runtime.'
    echo
    echo 'Usage: virtual <environment> [dir]'
    echo
    echo 'Arguments:'
    echo '  environment The environment to setup'
    echo
    echo 'Options:'
    echo '  -h, --help  Print this help message'
end

function fetch
    curl -L# $argv[1] | tar -zxf - --strip-components 1 -C $argv[2]
end

if not set -q argv[1]; or string match -qr '(-h|--help)' -- "$argv[1]"
    help
    functions -e help
    exit
end

# args
set environment $argv[1]
set dir (
    if set -q argv[2]
        echo $argv[2]
    else
        echo '.venv'
    end
)

mkdir -p $dir

# setup environment
switch $environment
    case node
        set -l ver (curl -sL 'https://semver.io/node/unstable')
        set -l yarn_ver (curl -sL 'https://semver.io/yarn/unstable')

        echo "Fetching Node $ver"
        fetch "https://nodejs.org/dist/v$ver/node-v$ver-linux-x64.tar.gz" $dir

        echo "Fetching Yarn $yarn_ver"
        fetch "https://github.com/yarnpkg/yarn/releases/download/v$yarn_ver/yarn-v$yarn_ver.tar.gz" $dir

        # remove provided npm
        rm -rf $dir/{bin/npm,lib/node_modules}

        # patch yarn to use $dir as $HOME
         sed -i "s|  node|  HOME=$PWD/$dir node|g" $dir/bin/yarn

         # install ncu and latest npm
         env PATH=(string join : $PATH):$PWD/$dir/bin yarn global add \
            npm-check-updates \
            npm@next

        echo -e \
"
function deactivate
    set -e VIRTUAL_ENV
    if set -q OLD_PATH
        set -gx PATH \$OLD_PATH
        set -e OLD_PATH
    end
    functions -e deactivate yarn
end


set -gx VIRTUAL_ENV \$PWD/$dir
set -gx OLD_PATH \$PATH
set -gx PATH \$PATH \$PWD/$dir/bin

if type -q yarn
    alias yarn 'env HOME=\$PWD/$dir yarn'
end

set -gx PATH \$PATH (yarn bin) > /dev/null ^ /dev/null
" > $dir/activate.fish

    case go
        set -l url (curl -sL 'https://golang.org/dl/' | grep -m1 linux | string match -r 'href="(http.*)"')[2]
        echo "Fetching Go:"
        fetch $url $dir

    echo -e \
"
function deactivate
    set -e VIRTUAL_ENV
    set -e GOPATH
    set -e GOROOT
    if set -q OLD_PATH
        set -gx PATH \$OLD_PATH
        set -e OLD_PATH
    end
    functions -e deactivate gopath
end

function gopath
    if not set -q argv[1]
        if set -q GOPATH
            cd \$GOPATH
        end
    else
        if set -q GOPATH; and set -l i (contains -i \$GOPATH/bin \$PATH)
            set -e PATH[\$i]
        end
        set -gx GOPATH \$argv[1]
        set -gx PATH \$PATH \$GOPATH/bin > /dev/null ^ /dev/null
    end
end

set -gx VIRTUAL_ENV \$PWD/$dir
set -gx GOROOT \$PWD/$dir
set -gx OLD_PATH \$PATH
set -gx PATH \$PATH \$GOROOT/bin

echo 'Use the gopath function to cd to or set GOPATH'
" > $dir/activate.fish

    case python
        python -m venv $dir

    case '*'
        echo "Unsupported environment: $argv[1]"
        exit -1
end

