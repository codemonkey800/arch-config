#!/usr/local/bin/fish

function applescript
  set script_name $argv[1]
  set -e argv[1]

  set script $DOTFILES/share/vpn/$script_name.template.applescript

  set old_ifs $IFS
  set IFS ''
  set script (cat $script)
  set script (printf $script $argv)
  set IFS $old_ifs

  echo $script | osascript
end

function connect_to_vpn
  applescript 'connect-to-vpn' 'jeremy-dev-vpn' > /dev/null
end

function disconnect_from_vpn
  applescript 'disconnect-from-vpn' 'jeremy-dev-vpn' > /dev/null
end

function is_connected_to_vpn
  scutil --nc list | grep 'jeremy-dev-vpn' | grep -q 'Connected'
end

function main
  set command $argv[1]

  if test (uname) != 'Darwin'
    echo 'This command is only supported on macOS.'
    return -1
  end

  if test -z $command
    if is_connected_to_vpn
      set command 'off'
    else
      set command 'on'
    end
  end

  switch $command
    case 'on'
      if is_connected_to_vpn
        echo 'Already connected to VPN!'
        return -1
      end

      connect_to_vpn
    case 'off'
      if not is_connected_to_vpn
        echo 'Already disconnected from VPN!'
        return -1
      end

      disconnect_from_vpn
    case '*'
      echo 'Usage: vpn [on|off]'
      echo
      echo 'Note:'
      echo '  - Not specifying a command will toggle the VPN status.'
  end
end

main $argv

