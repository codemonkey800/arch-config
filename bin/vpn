#!/usr/local/bin/fish

function run_script
  set script_name $argv[1]
  set -e argv[1]

  set script $DOTFILES/share/vpn/$script_name.template.applescript

  cat $script \
    | sed "s/\${SERVICE}/jeremy-dev-vpn/g" \
    | osascript
end

function connect_to_vpn
  run_script 'connect-to-vpn' > /dev/null
end

function disconnect_from_vpn
  run_script 'disconnect-from-vpn' > /dev/null
end

function is_connected_to_vpn
  scutil --nc list | grep 'jeremy-dev-vpn' | grep -q 'Connected'
end

function main
  set command $argv[1]

  if test (uname) != 'Darwin'
    echo 'This command is only supported on macOS.'
    return -1
  end

  if test -z $command
    if is_connected_to_vpn
      set command 'off'
    else
      set command 'on'
    end
  end

  switch $command
    case 'on'
      if is_connected_to_vpn
        echo 'Already connected to VPN!'
        return -1
      end

      connect_to_vpn
    case 'off'
      if not is_connected_to_vpn
        echo 'Already disconnected from VPN!'
        return -1
      end

      disconnect_from_vpn
    case '*'
      echo 'Usage: vpn [on|off]'
      echo
      echo 'Note:'
      echo '  - Not specifying a command will toggle the VPN status.'
  end
end

main $argv

